#! /usr/local/bin/perl -w 

=pod

=head1 NAME

load_repeats_from_gff.pl - load AGI repeats from gff into EnsemblDB

=head1 SYNOPSIS

  load_repeats_from_gff.pl [options] gff_file

Options:

  -h|--help             Show brief help and exit.
  -m|--man              Show detailed help
  -e|--ensembl_registry Path to Ensembl registry file.
  -s|--species          Species key in Ensembl registry file.
  -l|--logic_name       the logic name for the gene track
  -n|--no_insert        Do not make changes to the database. For debug.


=head1 OPTIONS

B<-h|--help>
  Print a brief help message and exits.

B<-m|--man>
  Print man page and exit

B<-e|-ensembl_registry>
  Use this Ensembl registry file for database connection info.
  Default is <ENSEMBLHOME>/conf/ensembl.registry

B<-s|--species>
  Use this species entry from the registry file [REQUIRED].

B<-l|--logic_name>
  the logic name for these set of genes, the track name shown on the contigview, default = ensembl


B<-n|--no_insert>
  Do not insert anything into the database. I.e. read-only mode.
  Useful for debug.


=head1 DESCRIPTION

load repeat features from the gff files similar to the following, it is generated by AGI for Oryza glaberrima whole genome

##gff-version 3
10      LTRharvest      LTR_retrotransposon     1998    6986    .       +       .       ID=RLX02146B73v400001;Name=RLX02146B73v400001_NA_LTRsimilarity91.06
10      LTRharvest      LTR_retrotransposon     32586   50712   .       +       .       ID=RLX02147B73v400001;Name=RLX02147B73v400001_NA_LTRsimilarity99.6
10      LTRharvest      LTR_retrotransposon     56539   65669   .       -       .       ID=RLC00001B73v400001;Name=RLC00001B73v400001_ji_LTRsimilarity95.71

repeat_name= "RLX02146B73v400001" (feature-ID)
repeat_class= "LTR/retrotransposon" 
repeat_type= "LTRs" 
repeat_consensus= "RLX02146B73v400001_NA_LTRsimilarity91.06" (this is a overload, what should go here is the consensus sequence of the repeat class)


B<The Ensembl Registry>

  The database connection details for both Ensembl and interproscan
  databases must be configured in an ensembl registry file.

  An example registry file would be;
  ---
  use Bio::EnsEMBL::DBSQL::DBAdaptor; 
  use Bio::EnsEMBL::Registry;
  # The Ensembl core database
  Bio::EnsEMBL::DBSQL::DBAdaptor->new
  ( '-species' => 'maize', 
    '-group'   => 'core', 
    '-dbname'  => 'zea_mays_core_30_bac20', );
  ---

B<Restoring the database>

  If the script bombs out half-way through, your database will be in a
  partial loaded state (i.e. in a bit of a mess). Here is some SQL to
  help put it right;

  delete rf.*, rc.* from repeat_feature rf join repeat_consensus rc using (repeat_consensus_id); 
  
Maintained by Sharon Wei <weix@cshl.edu>

=cut



use strict;
use warnings;
use Data::Dumper qw(Dumper);
use File::Basename;
use FindBin qw( $Bin );
use Pod::Usage;
use Getopt::Long;
use IO::File;

#//Uniq Types            repeat_type     repeat_class    repeat_consensus
#helitron                Type II Transposons             RC/Helitron
#LTR_retrotransposon     LTRs            LTR_retrotransposon
#SINE_element    => Type I Transposons/SINE      SINE
#terminal_inverted_repeat_element        Type II Transposons     terminal_inverted_repeat_element

our $BASEDIR ="/usr/local/";
use Readonly;
Readonly my $repeat_key_name => "id";
Readonly my $repeat_type_LTR => "LTRs";
Readonly my $repeat_key_consensus => "name";
Readonly my %repeat_type_hash => ('helitron' => 'Type II Transposons',
				  'LTR_retrotransposon' => 'LTRs',
				  'SINE_element' => 'Type I Transposons/SINE',
				  'terminal_inverted_repeat_element' => 'Type II Transposons',
);
Readonly my %repeat_class_hash => ('helitron' => 'RC/Helitron',
				  'LTR_retrotransposon' => 'LTR_retrotransposon',
				  'SINE_element' => 'SINE',
				  'terminal_inverted_repeat_element' => 'terminal_inverted_repeat_element',
);

BEGIN{
  # Set the perl libraries 
  unshift @INC, $BASEDIR.'/ensembl-live/ensembl/modules';
  unshift @INC, $BASEDIR.'/ensembl-live/ensembl-compara/modules';
  #unshift @INC, $BASEDIR.'/bioperl-live';
  unshift @INC, '/usr/local/ensembl-live/ensembl/modules';
}

use Bio::EnsEMBL::DBSQL::DBAdaptor;
use Bio::EnsEMBL::Registry;
use Bio::EnsEMBL::Analysis;
use Bio::EnsEMBL::RepeatFeature;
use Bio::EnsEMBL::RepeatConsensus;

use Date::Calc;


use vars qw( $ENS_DBA $I $INSERT $GFF_HANDLE $ANALYSIS $LOGIC_NAME );

my $date = time(); ;
my $SPECIES_NAME;

BEGIN{  #Argument Processing
  my $help=0;
  my $man=0;
  my ( $species,  $reg, $logic_name, $no_insert);
  GetOptions
      ( 
        "help|?"             => \$help,
        "man"                => \$man,
        "species=s"          => \$species,
        
        "ensembl_registry=s" => \$reg,
        "logic_name=s"       => \$logic_name,
        "no_insert"          => \$no_insert,

        )
      or pod2usage(2);
  pod2usage(-verbose => 2) if $man;
  pod2usage(1) if $help;

  # Process args

  my $gff_file = $ARGV[0] || pod2usage("\nNeed the path to a gff file\n");
  $species    || pod2usage("\nNeed a --species\n");
  $reg        ||= $BASEDIR.'/conf/ensembl.registry';
  $logic_name ||= 'ensembl';

  map{
    -e $_ || pod2usage( "\nFile $_ does not exist\n" );
    -r $_ || pod2usage( "\nCannot read $_\n" );
    -f $_ || pod2usage( "\nFile $_ is not plain-text\n" );
    -s $_ || pod2usage( "\nFile $_ is empty\n" );
  } $reg, $gff_file;

  # Put stuff in the database?
  $I= $no_insert ? 0 : 1; 

  # Load the ensembl file
  Bio::EnsEMBL::Registry->load_all( $reg );
  $ENS_DBA = Bio::EnsEMBL::Registry->get_DBAdaptor( $species, 'core' );
  $ENS_DBA || ( print( "No core DB for $species set in $reg\n" ) &&
                pod2usage() );

  # Create the analysis
  $ANALYSIS = Bio::EnsEMBL::Analysis->new
      ( -logic_name => $logic_name ); #'GeneModel_RiceIndica_BGI' 
  my $analAdaptor = $ENS_DBA->get_adaptor('Analysis');
  $analAdaptor->store ($ANALYSIS);

  $LOGIC_NAME = $logic_name;

  # Create a GFF stream
  $GFF_HANDLE = IO::File->new("< $gff_file")
      or die( "Could not read $gff_file: $!" );


  print( "Loading repeatFeature for $species\n" );
  my $pre_text = "  Target DB: ";
  foreach my $dba( $ENS_DBA ){
    print( $pre_text.$dba->dbc->dbname ."@". $dba->dbc->host .
          ":". $dba->dbc->port ."\n");
    $pre_text = "  Old DB: ";
  }

   $SPECIES_NAME = $species;
}

my $sa  = $ENS_DBA->get_adaptor('Slice');
my $rfa = $ENS_DBA->get_adaptor('RepeatFeature');
my $rca = $ENS_DBA->get_adaptor('RepeatConsensus');
my $rc_name = $LOGIC_NAME || 'no-name';

my $cnt = 0;
#my $repeat_type = $repeat_type_LTR;

while( my $line = $GFF_HANDLE->getline ){
  # Skip comment and empty lines
  next if ( $line =~ /\#/ ); 
  next if ( $line =~ /^\s+/ );
  chomp $line;

  #print "$line\n";  #####
  #warn(Dumper($TRPT2GENE));

  # Split gff line,\
  # start always <= end even for - strand genes
  my( $seqname, $source, $feature, 
      $start, $end, $score, $strand, $frame, 
      $attribute ) = split( /\s+/, $line, 9 );
  #$feature = uc($feature);
  next if $feature eq 'match_part';

  $strand = $strand eq '-' ? -1: 1;
 
  my $repeat_type = $repeat_type_hash{$feature};
  my $repeat_class = $repeat_class_hash{$feature};

#  $repeat_class =~ s/_/\//;
  my ($repeat_name,  $repeat_consensus) = ('unknown',  'unknown');
  my %attribs;
  ATTRIB: foreach my $id( split( /;/, $attribute ) ){

    if( $id  =~ m/([^=]*)=(.*)/ ){
	##gff-version 3
        #10      LTRharvest      LTR_retrotransposon     32586   50712   .       +       .       ID=RLX02147B73v400001;Name=RLX02147B73v400001_NA_LTRsimilarity99.6
        #10      LTRharvest      LTR_retrotransposon     56539   65669   .       -       .       ID=RLC00001B73v400001;Name=RLC00001B73v400001_ji_LTRsimilarity95.71

      	my $key   = lc $1;
	my $value = $2;
      	$value =~ s/\%([0-9]{2,4})/pack('C', hex($1))/seg;
      	$value =~ s/^"//;
      	$value =~ s/"$//;
      	$attribs{$key} = $value;
      	print "$key => $value\n";
    }
  }

  $repeat_name = $attribs{$repeat_key_name} ;
  $repeat_consensus = $attribs{$repeat_key_consensus} ;

  print "name=$repeat_name, repeat_type=$repeat_type, repeat_class=$repeat_class, repeat_consensus=$repeat_consensus, source=$source\n";
  
  my $rc = $rca->fetch_by_name_class($repeat_name, $repeat_class);
	
  unless ($rc){	
    $rc = Bio::EnsEMBL::RepeatConsensus->new
      (
       -NAME => $repeat_name,
       -REPEAT_CLASS => $repeat_class,
       -REPEAT_TYPE => $repeat_type,
       -REPEAT_CONSENSUS => $repeat_consensus,
       -ADAPTOR => $rca,
       );
    $rca->store( $rc ) if $I;
  }
  my $slice = $sa->fetch_by_region( undef, $seqname ) or 
      die "cannot fetch_by_region for $seqname\n";

  if( $start > $end ) {
      print "ERROR: start bigger than end! start=$start, end=$end, skip\n$line\n"; 
      next;
  }
  my $repeat_feature = Bio::EnsEMBL::RepeatFeature->new 
      (
       -start   => $start,
       -end     => $end,
       -strand  => $strand,
       -slice   => $slice,
       -analysis => $ANALYSIS,
       -repeat_consensus => $rc,
       -hstart  => 1,
       -hend    => $end-$start+1,
       -hstrand => 1,
       
       );


    $rfa->store( $repeat_feature ) if $I;

    $cnt++;
    print STDERR "stored $cnt repeat features: $start, $end, $attribs{id}\n";


}

print "Stored $cnt repeats from $ARGV[0]\n";
